' a 'wordle' like game
' you get 10 goes to guess a 5 letter word

stats_file = "./stats.txt"
cr = chr(10)

valid_words = readlines("./samples/validwords.txt")

result = main()
if result < 10 then
    print("You got it in ", false, "green")
    print(result+1)
end
save_stats(result)
print_stats()

function save_stats(result)
    max_streak = val(setting_get("max_streak"))
    streak = val(setting_get("streak"))
    value = 0
    fails = 0
    if result < 10 then
        streak = streak + 1
        value = val(setting_get(result))
        setting_set(result, value + 1)
    else
        streak = 0
        fails = val(setting_get("fails"))
        fails = fails + 1
        setting_set("fails", fails)
    end
    setting_set("streak", streak)
    if streak > max_streak then
        setting_set("max_streak", streak)
    end   
end

function print_stats()
    print("================")
    print("Streak: {setting_get("streak")}")
    print("Max Streak: {setting_get("max_streak")}")
    
    i = 0
    total = 0
    while i < 10
        num = val(setting_get(i))
        total = total + num
        j = 0
        print("{i+1} ", false)
        while j < num
            print("*", false)
            j=j+1
        end
        print("")
        i=i+1
    end
    fails = val(setting_get("fails"))
    print("Fails: {fails}")
    print("Win ratio: {total / (total + fails) * 100,"N2"}%")
end

function main()

    words = readlines("./samples/wordlist.txt")
    index = rand() * len(words)
    answer = left(words[index],5)
    'print(answer)
    
    i = 0
    while i < 10
        print("GUESS " + left(i+1,2))
        word = get_word()
        'print("word is " + word)

        if word > answer then
            print(word + " is greater than the answer")
        else 
            if word < answer then
                print(word + " is less than the answer")
            else
                i exit
            end
        end
        i=i+1
        show_matched(word, answer)
        print("")
        print("-------------------------------------")
        print("")
    end
    print("The answer is: " + answer)
    print("You didn't get it!", true, "red")
    i
end

function binary_search(word)
    l = 0
    r = len(valid_words) - 1
    while l <= r
        m = floor((l + r) / 2)
        if valid_words[m] < word then
            l = m + 1
        else
            if valid_words[m] > word then
                r = m - 1
            else
                true exit
            end 
        end      
    end
    false
end

function validate_word(word)
    'print (len(word))
    if len(word) <> 5 then
        false
        exit
    end

    binary_search(word)
end

function get_word()
    word = input("Enter a 5 letter word > ")
    'print("word entered is " + word)
    while not validate_word(word)
        print("Not a valid 5 letter word!")
        word = input("Enter a 5 letter word > ")
    end
    word
end

function show_matched(guess, answer)
    'print(guess)
    print("Answer: ", false)
    i = 1
    while i <= 5
        'print (mid(guess,i,1) + " - " + mid(answer,i,1))
        if mid(guess,i,1) == mid(answer,i,1) then
            print(mid(answer,i,1), false)
        else
            print("?", false)
        end
        i = i + 1
    end
    print("")
end

' *** MAIN ***

tab = 0
you = 1000
horses = load_horses(10)
while true	
	race_result = main()
	you = you - race_result[0]
	you = you + race_result[1]

	tab = tab + race_result[0]
	tab = tab - race_result[1]

	print("You ${you,"N2"}, TAB ${tab,"N2"}")
	q = input("Press Enter for another race, q to quit")
	if q == "q" then
		exit
	end
	horses = load_horses(10)
end
' *** END ****

function load_horses(num)
	csv = readlines("./samples/horses.txt").shuffle()
	result = array()
	if num > len(csv) then num = len(csv) end
	for i=0 to num-1
		result.push(csv[i].split(","))
	next
	result
end

function main()
	winnings = 0
	winners = dim(len(horses))
	print("Calculating odds.  Please wait...")
	for i = 1 to 2000
		winner = run_race(dummy,0)
		winners[winner] = winners[winner] + 1
	next
	print(len(winners))
	odds = get_odds(winners)
	print_odds(odds)
	inp = get_bet()
	horse_bet = inp[0]
	amount_bet = inp[1]
	clear()
	winner = run_race(print_horses, horse_bet)
	print("Race Finished. Horse {horses[winner].name()} won!")
	if horse_bet == winner then
		winnings = odds[horse_bet] * amount_bet
		print("you win! you get ${winnings,"N2"}")
	end

	array(amount_bet, winnings)
end

function print_odds(odds)
	clear()
	for i = 0 to len(horses)-1
		print("{chr(65+i)}  {horses[i].name()}", false, "default", 1,1 +i)
		print("${odds[i],"6.2"}", false, "default", 30,1+i)
	next
	print("")	
end

function get_bet()
	h = input("What horse do you want to win?")
	h = asc(ucase(h)) - 65
	bet = input("How much to you want to bet?")
	array(h,val(bet))
end

function get_odds(results)
	odds = array()
	total = 2000
	for i = 0 to len(results)-1
		if results[i] == 0 then
			odds.push(100)
		else
			odds.push((total/results[i]) * 0.8)
		end
	next
	odds
end

function name(a)
	a[0]
end

function speed(a)
	val(a[1])
end

function dummy(x,y) 0 end

function print_horses(race, horse_bet)
	print("",false,"normal",1,1)
	for i = 0 to 20
		for h = 0 to len(horses) - 1
			color = match h 
				when horse_bet then "yellow" 
				else "default" 
			end
			if round(race[h],0) == i then print (" {chr(65+h)} ", false, color) else print("   ", false) end
		next
		print("")
	next
	sleep(500)
	'input("Enter to continue...")
end

function run_race(print_fn, horse_bet)
	race = dim(len(horses))
	while true
		print_fn(race, horse_bet)
		for h = 0 to len(horses) - 1
			race[h] = race[h] + horses[h].speed() * rand()
			'if race[h] > 20 then finished = true end
		next
		m = max(race)
		if m >= 20 then 
			race.find(m) 
			exit
		end
	end
end
